/*
 * Created by @UnbarredStream on 19/06/22 13:39
 * Copyright (c) 2022 . All rights reserved.
 * Last modified 19/06/22 13:37
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'knf.hydra.core'
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$rootDir/docs"))
    dokkaSourceSets {
        named("main") {
            includes.from("README.md")
            noAndroidSdkLink.set(false)
            sourceLink {
                remoteUrl.set(java.net.URL("https://github.com/jordyamc/hydra_core/tree/master/core/src/main/java"))
                remoteLineSuffix.set("#L")
            }
        }
    }
}

dependencies {
    //dokkaHtmlPlugin 'org.jetbrains.dokka:kotlin-as-java-plugin:1.5.30'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'com.github.jordyamc:Cloudflare-Bypasser:1.0.19'
    api 'androidx.paging:paging-runtime-ktx:3.1.1'
    api 'androidx.room:room-runtime:2.4.2'
    api 'androidx.room:room-ktx:2.4.2'
    kapt 'androidx.room:room-compiler:2.4.2'
    api 'androidx.preference:preference-ktx:1.2.0'
    api 'androidx.annotation:annotation:1.4.0'
    api 'com.google.code.gson:gson:2.8.9'
    api 'com.github.jordyamc:Gson-ktx:1.0'
    api 'de.prosiebensat1digital.oasis-jsbridge-android:oasis-jsbridge-quickjs:0.14.5'
}

task dokkaJavadocJar(type: Jar) {
    dependsOn(tasks.dokkaJavadoc)
    def directories = new ArrayList<Object>()
    tasks.dokkaJavadoc.each {
        directories.add(it.outputDirectory)
    }
    from(directories)
    archiveClassifier.set("javadoc")
}

task dokkaHtmlJar(type: Jar) {
    dependsOn(tasks.dokkaHtml)
    def directories = new ArrayList<Object>()
    tasks.dokkaHtml.each {
        directories.add(it.outputDirectory)
    }
    from(directories)
    archiveClassifier.set("html-doc")
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives dokkaJavadocJar
    archives dokkaHtmlJar
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = uri("$buildDir/repository")
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'knf.hydra'
                artifactId = 'core'
                version = '1.0-rc1'
                artifact(androidSourcesJar)
                artifact(dokkaJavadocJar)
                artifact(dokkaHtmlJar)
            }
        }
    }
}